package uk.ac.imperial.lpgdash

import java.util.List
import java.util.ArrayList

import uk.ac.imperial.lpgdash.ProvisionPool
import uk.ac.imperial.lpgdash.actions.*
import uk.ac.imperial.lpgdash.facts.*
import uk.ac.imperial.lpgdash.RoundType
import uk.ac.imperial.lpgdash.allocators.LegitimateClaims

global org.apache.log4j.Logger logger

declare LoggedHistory
	@role( event )
	act: Object @key
end

query lcCluster(Cluster c)
	Cluster( this == c, allocationMethod in (
		Allocation.LC_FIXED,
		Allocation.LC_F1a,
		Allocation.LC_F1b,
		Allocation.LC_F1c,
		Allocation.LC_F2,
		Allocation.LC_F3,
		Allocation.LC_F4,
		Allocation.LC_F5,
		Allocation.LC_F6,
		Allocation.LC_SO ) )
end

query compliantRound(Cluster c, Player p, int $t)
	MemberOf( player == p, cluster == c )
	Generate( t == $t, $g : g, $q : q, player == p )
	Demand( $d : quantity, player == p, t == $t, quantity <= $q )
	Provision( $p : quantity, player == p, t == $t, quantity >= $g )
	Allocate( $r : quantity, player == p, t == $t )
	Appropriate( $rP : quantity, player == p, t == $t, quantity <= $r )
end

rule "Prune LoggedHistory"
	when
		$l : LoggedHistory( $a : act )
		not Object( this == $a )
	then
		retract( $l );
end

rule "Create PlayerHistory"
	salience 2
	when
		MemberOf( $p : player, $c : cluster )
		lcCluster( $c ;)
		Player( this == $p, getHistory()[$c] == null )
	then
		modify( $p ) {
			getHistory().put( $c, new PlayerHistory( ) )
		}
end

rule "Update allocation history new"
	when
		Round( $t : number )
		MemberOf( $p : player, $c : cluster )
		lcCluster( $c ;)
		$a : Allocate( player == $p, $q : quantity, t == $t )
		not LoggedHistory( $a ;)
	then
		PlayerHistory hist = $p.getHistory().get($c);
		hist.allocations.addValue( $q );
		if($q > 0)
			hist.incrementRoundsAllocated();
		insert( new LoggedHistory( $a ) );
end

rule "Update demand history new"
	when
		Round( $t : number )
		MemberOf( $p : player, $c : cluster )
		lcCluster( $c ;)
		$d : Demand( player == $p, $q : quantity, t == $t )
		not LoggedHistory( $d ;)
	then
		PlayerHistory hist = $p.getHistory().get($c);
		hist.demands.addValue( $q );
		insert( new LoggedHistory( $d ) );
		//logger.info( "Average Demand: "+ hist.demands.getMean() +", "+ $p );
end

rule "Update provision history new"
	when
		Round( $t : number )
		MemberOf( $p : player, $c : cluster )
		lcCluster( $c ;)
		$d : Provision( player == $p, $q : quantity, t == $t )
		not LoggedHistory( $d ;)
	then
		PlayerHistory hist = $p.getHistory().get($c);
		hist.provisions.addValue( $q );
		hist.incrementRoundsParticipated();
		insert( new LoggedHistory( $d ) );
		//logger.info( "Average Provision: "+ hist.provisions.getMean() +", "+ $p );
		//logger.info( "Rounds participated: "+ hist.getRoundsParticipated() +", "+ $p );
end

rule "Count rounds as head new"
	when
		Round( $t : number )
		MemberOf( $p : player, $c : cluster )
		lcCluster( $c ;)
		$h : IsHead( player == $p, cluster == $c, time == $t )
		not LoggedHistory( $h ;)
	then
		PlayerHistory hist = $p.getHistory().get($c);
		hist.incrementRoundsAsHead();
		insert( new LoggedHistory( $h ) );
		//logger.info( "Rounds as head: "+ hist.getRoundsAsHead() +", "+ $p );
end

rule "Count compliant rounds new"
	when
		MemberOf( $p : player, $c : cluster )
		lcCluster( $c ;)
		Round( $t : number, type == RoundType.APPROPRIATE )
		compliantRound( $c , $p, $t ;) 
	then
		PlayerHistory hist = $p.getHistory().get($c);
		hist.incrementCompliantRounds();
		//logger.info( "Compliant rounds: "+ hist.getCompliantRounds() +", "+ $p );
end

rule "Calculate satisfaction from previous round"
	when
		MemberOf( $player : player, $c : cluster )
		lcCluster( $c ;)
		Round( $t : number, type == RoundType.DEMAND )
		Generate( $g : g, $q : q, player == $player, t == $t-1 )
		Demand( $d : quantity, player == $player, t == $t-1 )
		Provision( $p : quantity, player == $player, t == $t-1 )
		Appropriate( $r : quantity, player == $player, t == $t-1 )
	then
		PlayerHistory hist = $player.getHistory().get($c);
		double s = hist.getSatisfaction();
		double R = $r + ( $g - $p );
		if( R >= $q ) {
			s = s + $player.getAlpha() * (1 - s);
		} else {
			s = s - $player.getBeta() * s;
		}
		hist.setSatisfaction(s);
end

rule "LC resource allocation"
	no-loop
	salience -2
	when
		$r : Round( type == RoundType.DEMAND, $t : number, $tm : number - 1 )
		not Processed( $r ;)
		$c : Cluster( $method : allocationMethod )
		lcCluster( $c ;)
		$pool : ProvisionPool( cluster == $c )
		$poolMembers : List(size > 0) from accumulate( MemberOf( cluster == $c, $p : player ), collectList( $p ) )
		$lc : LegitimateClaims( c == $c )
	then
		logger.info( $method +" allocation for resources " + $c);
		$lc.allocate($poolMembers, $pool.getQuantity(), $t );
		modify( $pool ) {
			setQuantity(0);
		}
		insert( new Processed( $r ) );
end
